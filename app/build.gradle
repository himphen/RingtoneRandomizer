apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "hibernate.v2.ringtonerandomizer"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 213100
        versionName "3.1.0"
        multiDexEnabled true
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            useProguard false
            buildConfigField "String", "ADMOB_KEY", "\"" + getSecretKey("ADMOB_KEY") + "\""
            buildConfigField "String", "ADMOB_APP_ID", "\"" + getSecretKey("ADMOB_APP_ID") + "\""
            resValue "string", "ADMOB_APP_ID", getSecretKey("ADMOB_APP_ID")
            buildConfigField "String[]", "ADMOB_DEVICE_ID", getSecretKey("ADMOB_DEVICE_ID")
            buildConfigField "String", "GOOGLE_IAP_KEY", "\"" + getSecretKey("GOOGLE_IAP_KEY") + "\""
            buildConfigField "String", "CONTACT_EMAIL", "\"" + getSecretKey("CONTACT_EMAIL") + "\""
            resValue "string", "CONTACT_EMAIL", getSecretKey("CONTACT_EMAIL")
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "ADMOB_KEY", "\"" + getSecretKey("ADMOB_KEY") + "\""
            buildConfigField "String", "ADMOB_APP_ID", "\"" + getSecretKey("ADMOB_APP_ID") + "\""
            resValue "string", "ADMOB_APP_ID", getSecretKey("ADMOB_APP_ID")
            buildConfigField "String[]", "ADMOB_DEVICE_ID", getSecretKey("ADMOB_DEVICE_ID")
            buildConfigField "String", "GOOGLE_IAP_KEY", "\"" + getSecretKey("GOOGLE_IAP_KEY") + "\""
            buildConfigField "String", "CONTACT_EMAIL", "\"" + getSecretKey("CONTACT_EMAIL") + "\""
            resValue "string", "CONTACT_EMAIL", getSecretKey("CONTACT_EMAIL")
        }
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

static def getSecretKey(name) {
    Properties props = new Properties()
    File f = new File('./../Key/ringtonerandomizer.secrets.properties')
    if (f.exists()) {
        props.load(new FileInputStream(f))
        return props[name]
    } else {
        return name
    }
}

dependencies {
    // Google Android Support
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:preference-v14:28.0.0'

    // Google Play Services
    implementation 'com.google.firebase:firebase-core:16.0.7'
    implementation 'com.google.firebase:firebase-ads:17.2.0'

    // https://github.com/JakeWharton/butterknife
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    // Material Dialog
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'

    // Simple IAB
    implementation 'com.anjlab.android.iab.v3:library:1.0.44'

    // https://github.com/Blankj/AndroidUtilCode
    implementation 'com.blankj:utilcode:1.23.7'

    // Crashlytics
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.9'

    // https://github.com/javiersantos/AppUpdater
    implementation 'com.github.javiersantos:AppUpdater:2.7'
}

apply plugin: 'com.google.gms.google-services'